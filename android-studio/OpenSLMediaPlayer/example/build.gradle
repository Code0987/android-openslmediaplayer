/*
 *    Copyright (C) 2014 Haruki Hasegawa
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'com.android.application'

ext {
    projectBasePath = file('../../../example').absolutePath
    ndkProjectCodeRootDir = projectBasePath // required for 'android-setup-custom-ndk-build.gradle'
}

// Common configurations 
apply from: '../common/android-set-common-properties.gradle'
apply from: '../common/android-use-legacy-layout-files.gradle'
apply from: '../common/android-disable-default-ndk-build.gradle'
apply from: '../common/android-signing-utils.gradle'

// Use eclipse style project files layout
useLegacyLayoutFiles(android.sourceSets.main, projectBasePath)

android {
    defaultConfig {
        applicationId "com.h6ah4i.android.example.openslmediaplayer"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "0.5.0"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            // NOTE: disable minify & shrinking easy to find out the problem
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


// load signing config from external properties file
if (loadSigningConfigFromPropertiesFile(
    project, android.signingConfigs.release, '../signing/example-signing.properties')) {
    android.buildTypes.release.signingConfig android.signingConfigs.release  
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:21.0.+'
    compile 'com.android.support:appcompat-v7:21.0.+'
    compile 'com.android.support:gridlayout-v7:21.0.+' // Needed for android.support.v7.widget.Space
    

    compile project(path: ':library')

    // WTF!  This does not work on New Build System 0.14.4 (2014/11/19)
    // releaseCompile project(path: ':library', configuration: 'release')
    // debugCompile project(path: ':library', configuration: 'debug')
}
