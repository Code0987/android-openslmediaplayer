/*
 *    Copyright (C) 2014 Haruki Hasegawa
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'com.android.library'

ext {
    projectBasePath = file('../../../library').absolutePath

    // required for 'android-setup-custom-ndk-build.gradle'
    ndkProjectCodeRootDir = projectBasePath

    // required for 'android-maven-publish.gradle'
    mavenPublishDestDir = new File(rootDir, "repository").absolutePath
    mavenPublishDataFile = file('./maven-publish-data.properties').absolutePath
    mavenPublishSigningSetting =  file('../signing/library-maven-publish-signing.properties').absolutePath
    useDoclava = true
    docLavaTemplateDir = project.file('doclava-template/template').absolutePath
}

// Common configurations 
apply from: '../common/android-set-common-properties.gradle'
apply from: '../common/android-use-legacy-layout-files.gradle'
apply from: '../common/android-setup-custom-ndk-build.gradle'

// Use eclipse style project files layout
useLegacyLayoutFiles(android.sourceSets.main, projectBasePath)

android {
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21

        def dataProps = new Properties()
        dataProps.load(project.file(project.mavenPublishDataFile).newDataInputStream())

        versionCode dataProps.VERSION_CODE.toInteger()
        versionName dataProps.VERSION_NAME
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            consumerProguardFiles  'proguard-rules.pro'
        }
        debug {
            jniDebuggable true

            minifyEnabled false
            shrinkResources false
            consumerProguardFiles  'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

// NOTE: this line should be place at end of the file
apply from: '../common/android-maven-publish.gradle'
